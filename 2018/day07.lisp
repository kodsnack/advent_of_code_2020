;;;; day07.lisp

(in-package #:aoc2018.day07)

(defun requirement (a b) (list a '-> b))
(defun anterior (req) (car req))
(defun posterior (req) (caddr req))
(defun names (req) (list (anterior req) (posterior req)))

(defun parse (line)
  (ppcre:register-groups-bind (((compose #'intern #'string-upcase) a b))
      ("Step (\\w) must be finished before step (\\w) can begin." line)
    (requirement a b)))

(defun node (c) (list c nil))
(defun name (node) (car node))
(defun anteriors (node) (cadr node))

(defun find-node (name nodes) (find name nodes :key #'name))
(defun add-anterior (node ante) (push ante (cadr node)))

(defun make-nodes (reqs)
  (let ((nodes (mapcar #'node (remove-duplicates (mapcan #'names reqs)))))
    (dolist (req reqs nodes)
      (add-anterior (find-node (posterior req) nodes)
                    (anterior req)))))

(defun name-sort (nodes) (sort nodes #'string< :key (compose #'symbol-name #'name)))
(defun free (nodes) (remove-if-not #'null nodes :key #'anteriors))
(defun next (nodes) (name-sort (free nodes)))
(defun clear (name node) (setf (cadr node) (delete name (cadr node))))

(defun determine-the-order (reqs)
  (loop for nodes = (make-nodes reqs) then (delete next nodes)
        for next = (first (next nodes))
        while next do (mapc (curry #'clear (name next)) nodes)
        collect (name next)))

(defun part1 (input)
  (format nil "狺ㄤ弭弪黹铄翳瀛矧溴磲疸狎＇疳蝮ㄡ镢红轭弩轭瘐舂┅┅ㄤ彐躅箦泔钿钺礤ǐ栋ūōㄣ栳颦泔溴ㄥ祠簌礅镬钺礤钺礤癌ㄣ栳颦泔溴＼俩┅┅ㄤ彐躅黠螂弪īㄣ镱铋铋飑ㄤ彐躅忮玳黠螂弪钺礤蝠灬汜黠螂弪钺礤蝠灬沅黠螂弪箦泔钿钺礤┅ㄤ彐躅黠螂黠螂弪麒孱箦泔钿蟓戾骠黠螂弪ㄤ邈ㄣ潋黠螂弪┅┅ㄤ彐躅翎箅黠螂弪ㄣ狎黠螂弪┅ㄤ彐躅箦泔钿蟓戾骠黠螂弪ㄣ潋黠螂弪┅ㄤ彐躅蝈箦黠螂弪蝠灬汜黠螂弪铋飑蝠灬沅黠螂弪铋飑ㄤ彐躅蝈狃黠螂弪螬祜镳骘轭黠螂弪殒ㄥ箦泔钿蟓戾骠鳗泔祆邈痱镧翎箅鳗蝈箦鳗┅ㄤ彐躅蝈徜黠螂弪螬蝈盹鲥殒铒＇铛祆黠螂弪弘妁＇翎箅┅ㄤ彐躅轭痱镧蝈篌黠螂弪螬磲疸狎＇汜黠螂弪螬ㄤ彐躅栾鳝祜铉鏖祆轸翎脲蝈耋祜镳鏖翳铒溴磲脲铒溴蝈耋鏖翳翎蜱弭戾铉翳铒溴螬鏖翳黠螂弪祜镳蝈疱狒泔祆邈黠螂弪┅骘翦篝骘痱镤鏖翳滹铄骘泔躅骝镯骘戾è泔眇戾翦蝈狃黠螂弪螬┅箦翩滹铄ㄡ痧孱滹铄泔眇戾翦洎ㄤ镬轶ㄣ泔眇戾翦洎磲疸狎ㄣ躜蝙＇沆遽悌铒溴螬┅骘铄磲疸狎＇钺礤铄铒溴螬麒殪戾铉翳滹铄翎蜱弭滹磲疸灬礅溽瞟ㄢ彗轭瞟箦翩铒溴蝈盹鲥铒溴弘妁＇钺礤┅蝈徜黠螂弪螬铄舂磲疸＇黠螂黠螂弪螬骈钺祆蝈趱蝾泔躅舂┅ㄤ彐躅疳螋ㄩ铕豸ㄨ秣祜铉鏖祆轸翎脲磲疸狎＇疳蝮ㄡ镢红轭弩轭瘐舂┅